PREFIX : <http://example.com/>
SELECT ?year ?s_nation ?p_category (SUM(?lo_revenue - ?lo_supplycost) AS ?sum_profit) 
WHERE {
	?li :orderdate ?lo_orderdate ;
	   :custkey ?lo_custkey ;
	   :partkey ?lo_partkey ;
	   :suppkey ?lo_suppkey ;
	   :revenue ?lo_revenue ;
	   :supplycost ?lo_supplycost .

	?lo_custkey :region 'AMERICA' .
	?lo_suppkey :region 'AMERICA' .
	?lo_suppkey :nation ?s_nation .
	?lo_partkey :mfgr ?p_mfgr .
	?lo_partkey :category ?p_category .  
	?lo_orderdate :year ?year .

	FILTER(?year = 1997 || ?year = 1998)
	FILTER(?p_mfgr = 'MFGR#1' || ?p_mfgr = 'MFGR#2')
}
GROUP BY ?year ?s_nation ?p_category 
ORDER BY ?year ?s_nation ?p_category